buildscript {
   repositories {
      mavenCentral()
      maven { url "http://dl.bintray.com/ihmcrobotics/maven-release" }
      maven { url "http://dl.bintray.com/ihmcrobotics/maven-vendor" }
      maven { url "https://plugins.gradle.org/m2/" }
      mavenLocal()
      jcenter()
   }
   dependencies {
      classpath "us.ihmc:ihmc-build:0.12.8"
      classpath "us.ihmc:ros2-msg-to-pubsub-generator:0.2"
      classpath "us.ihmc:ihmc-pub-sub-generator:0.3.7"
      classpath "org.ajoberstar:grgit:2.1.0"
   }
}
apply plugin: "us.ihmc.ihmc-build"
apply plugin: "org.ajoberstar.grgit"

def rclInterfacesPath = "src/main/vendor/rcl_interfaces"
def rclInterfacesUrl = "https://github.com/ros2/rcl_interfaces.git"
def commonInterfacesPath = "src/main/vendor/common_interfaces"
def commonInterfacesUrl = "https://github.com/ros2/common_interfaces.git"
def ros2Release = "0.4.0"

ihmc {
   loadProductProperties("../product.properties")
   version = "0.4.0.1"

   configureDependencyResolution()
   resourceDirectory("main", "custom-idl")
   resourceDirectory("main", "generated-idl")
   javaDirectory("main", "generated-java")
   resourceDirectory("main", "vendor")
   configurePublications()
}

mainDependencies {
   compile group: "us.ihmc", name: "euclid-core", version: "0.4.13"
   compile group: "us.ihmc", name: "euclid", version: "0.7.5"
   compile group: "us.ihmc", name: "ihmc-pub-sub", version: "0.5.1"
}

generatorDependencies {
   compile group: "us.ihmc", name: "ros2-msg-to-pubsub-generator", version: "0.3.0"
}

task show() {
   doLast {
      project.gradle.includedBuilds.each { println it }
      buildscript.configurations.classpath.each { println "Buildscript: " + it }
      configurations.compile.each { println "Compile: " + it }
   }
}

task generateMessages(type: us.ihmc.ros2.rosidl.RosInterfaceGeneratorTask) {
   doFirst {
      setupVendoredRepo(commonInterfacesPath, commonInterfacesUrl, ros2Release)
      setupVendoredRepo(rclInterfacesPath, rclInterfacesUrl, ros2Release)
   }

   rosPackages = files(rclInterfacesPath, commonInterfacesPath)
   outputDirectory = file("src/main/generated-java")
   customIDL = files("src/main/idl")
}

private void setupVendoredRepo(String clonePath, String vcsUrl, String tagName)
{
   delete(clonePath)

   if (!file(clonePath).exists())
   {
      org.ajoberstar.grgit.Grgit.clone {
         dir = file(clonePath)
         uri = vcsUrl
         refToCheckout = tagName
      }
   }
}